{ config, lib, pkgs, ... }:

let cfg = config.modules.desktop.i3;
in {
  options.modules.desktop.i3 = {
    enable = lib.mkEnableOption "i3";

    terminal = lib.mkOption {
      type = lib.types.str;
      default = "${pkgs.kitty}/bin/kitty";
    };
  };

  config = lib.mkIf cfg.enable {
    programs = {
      i3status-rust = {
        enable = true;
        bars.bottom = {
          icons = "awesome5";
          theme = "native";
          blocks = [
            {
              block = "networkmanager";
              interface_name_exclude = [ "br\\-[0-9a-f]{12}" "docker\\d+" ];
            }
            {
              block = "battery";
              interval = 10;
              format = "{percentage} {time}";
              allow_missing = true;
              hide_missing = true;
            }
            {
              block = "disk_space";
              path = "/";
              alias = "/";
              info_type = "available";
              unit = "GB";
              interval = 60;
              warning = 20.0;
              alert = 10.0;
              format = "{icon} {used} /{total}";
            }
            {
              block = "memory";
              display_type = "memory";
              format_mem = "{mem_used} /{mem_total}";
              format_swap = "{swap_used_percents}";
              interval = 10;
            }
            {
              block = "cpu";
              interval = 1;
            }
            { block = "sound"; }
            {
              block = "docker";
              interval = 10;
              format = "{running}";
            }
            {
              block = "time";
              interval = 60;
              format = "%a %d/%m %R";
            }
          ];
        };
      };

      rofi = {
        enable = true;
        cycle = true;
        font = "Iosevka 14";
        location = "center";
        terminal = "${pkgs.kitty}/bin/kitty";
        theme = ./rofi/kenran.rasi;
        extraConfig = { modi = "run,drun,ssh,window"; };
      };
    };

    home.packages = [ pkgs.font-awesome ];

    xsession.windowManager.i3 = {
      enable = true;
      config = rec {
        inherit (cfg) terminal;
        modifier = "Mod4";
        startup = [{
          command = "setxkbmap -layout us -variant altgr-intl";
          always = true;
        }];
        defaultWorkspace = "workspace number 0";
        menu = "rofi -disable-history -show run";
        window = {
          titlebar = false;
          border = 2;
        };
        keybindings = lib.mkOptionDefault {
          # Use normal vim keys for moving between windows.
          "${modifier}+h" = "focus left";
          "${modifier}+l" = "focus right";
          "${modifier}+j" = "focus down";
          "${modifier}+k" = "focus up";
          "${modifier}+Shift+h" = "move left";
          "${modifier}+Shift+l" = "move right";
          "${modifier}+Shift+j" = "move down";
          "${modifier}+Shift+k" = "move up";
          "${modifier}+v" = "split v";
          "${modifier}+g" = "split h";
          "${modifier}+t" = "exec ${terminal}";
          "${modifier}+space" = lib.mkForce "exec ${menu}";
          "${modifier}+d" = lib.mkForce "exec rofi -disable-history -show drun";
        };
        bars = [{
          position = "bottom";
          mode = "dock";
          # This file is automatically generated by the bar named
          # 'bottom'.
          statusCommand =
            "i3status-rs ~/.config/i3status-rust/config-bottom.toml";
          trayOutput = null;
          workspaceButtons = true;
          workspaceNumbers = true;
          hiddenState = "hide";
          fonts = {
            names = [ "Iosevka" "FontAwesome" ];
            size = 14.0;
          };
          colors = {
            background = "#040404";
            statusline = "#bbbbbb";
            separator = "#0ac80a";
            focusedWorkspace = {
              background = "#042004";
              border = "#0ac30a";
              text = "#0ac30a";
            };
            activeWorkspace = {
              background = "#040404";
              border = "#666666";
              text = "#0ac30a";
            };
            inactiveWorkspace = {
              background = "#040404";
              border = "#666666";
              text = "#909590";
            };
            bindingMode = {
              background = "#040404";
              border = "#ff4500";
              text = "#ff4500";
            };
            urgentWorkspace = {
              background = "#040404";
              border = "#ff4500";
              text = "#ff4500";
            };
          };
        }];
        colors = {
          focused = {
            background = "#042004";
            border = "#0ac30a";
            childBorder = "#007000";
            indicator = "#007000";
            text = "#0ac30a";
          };
          focusedInactive = {
            background = "#5f676a";
            border = "#333333";
            childBorder = "#5f676a";
            indicator = "#484e50";
            text = "#eeeeee";
          };
          placeholder = {
            background = "#040404";
            border = "#040404";
            childBorder = "#0c0c0c";
            indicator = "#040404";
            text = "#eeeeee";
          };
          unfocused = {
            background = "#222222";
            border = "#333333";
            childBorder = "#222222";
            indicator = "#292d2e";
            text = "#909590";
          };
          urgent = {
            background = "#040404";
            border = "#ff4500";
            childBorder = "#700000";
            indicator = "#700000";
            text = "#ff4500";
          };
        };
      };
    };
  };
}
